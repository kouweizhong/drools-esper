package memelet.droolsesper

import java.util.Map

dialect "mvel"

/*
 How do I use patterns to correlate events arriving in-order or out-of-order?
 
 http://esper.codehaus.org/tutorials/solution_patterns/solution_patterns.html#correlate-3
 
	select * from pattern [every s=StartEvent -> 
  		a=AbortedEvent(exchangeId = s.echangeId) where timer:within(30 sec)]
*/

global Map results

declare StartEvent
	@role(event)
	id         : String 
	exchangeId : String
end

declare AbortedEvent
	@role(event)
	id         : String 
	exchangeId : String
end

declare FinishedEvent
	@role(event)
	id         : String 
	exchangeId : String
end

rule "[every s->a] How do I use patterns to correlate events arriving in-order or out-of-order?"
when
	$se : StartEvent() 		
	$ae : AbortedEvent(exchangeId == $se.exchangeId, this after[1ms,30s] $se)
then
	results.put("startEvent", $se);
	results.put("abortedEvent", $ae);
	
	// We have to retract the StartEvent to similate esper's [every a->b]
	retract($se)
end
