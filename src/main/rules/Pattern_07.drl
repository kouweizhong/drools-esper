package memelet.droolsesper

import java.util.List
import java.util.Map

dialect "mvel"

/*
 How do I correlate 3 events in a time window in which events have similar properties?
 
 http://esper.codehaus.org/tutorials/solution_patterns/solution_patterns.html#correlate_pattern_similar_properties
 
	every trade1=Trade(userId in ('U1000','U1001','U1002') ) ->
  		(trade2=Trade(userId in ('U1000','U1001','U1002') and
     		userId != trade1.userId and ccypair = trade1.ccypair 
     		and direction = trade1.direction) ->
   		 trade3=Trade(userId in ('U1000','U1001','U1002') and 
     		userId not in (trade1.userId, trade2.userId) and
     		ccypair = trade1.ccypair and direction = trade1.direction))
  		) where timer:within(10 min)
*/

/*
 Drools not have a concept of a rule timer similar to the Esper 
 'where timer:within(..)'. This forces us to have to windows on
 the trade2 and trade3 clauses. I'm not clear yet whether semantics 
 are the same. 
 
 Interstingly, Drools does have the opposite of the Esper timer: 
 rule @duration. Seems that 
 
*/

global Map results

declare Trade
	@role(event)
	id        : String
	userId    : String
	ccypair   : String
	direction : String
end

rule "Correlate 3 events in a time window in which events have similar properties"
	@expires(10m) // TODO What does this mean? It seems to have no effect
when
	$trade1 : Trade(userId in ('U1000', 'U1001', 'U1002'))
	
	$trade2 : Trade(userId in ('U1000', 'U1001', 'U1002')
					&& userId != $trade1.userId
					&& ccypair == $trade1.ccypair && direction == $trade1.direction,
					this after[1ms,10m] $trade1)
					 
	$trade3 : Trade(userId in ('U1000', 'U1001', 'U1002')
					&& userId not in ($trade1.userId, $trade2.userId)
					&& ccypair == $trade1.ccypair && direction == $trade1.direction,
					this after $trade2,
					this after[1ms,10m] $trade1) // TODO Why is this needed?
		
then
	results.put("trade1", $trade1)
	results.put("trade2", $trade2)
	results.put("trade3", $trade3)
end

