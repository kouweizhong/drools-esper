package memelet.droolsesper

import java.util.Map
global Map results

dialect "mvel"

/*
 How do I correlate events arriving in 2 or more streams?
 
 http://esper.codehaus.org/tutorials/solution_patterns/solution_patterns.html#correlate
 
	select fraud.accountNumber as accntNum, withdraw.amount as amount         
	from FraudWarningEvent.win:time(30 sec) as fraud,
    	WithdrawalEvent.win:time(30 sec) as withdraw
	where fraud.accountNumber = withdraw.accountNumber
*/	

rule "Correlate events arriving in 2 or more streams?"
when
    $e : WithdrawalEvent($accountNumber : accountNumber, $amount : amount) over window:time(30s) 
    $f : FraudWarningEvent(accountNumber == $accountNumber) over window:time(30s) 
then
	System.out.println("e=" + $e.id + ", f=" + $f.id)
	results.put("accountNumber", $accountNumber);
	results.put("amount", $amount);
end

rule "Correlate events arriving in 2 or more streams? (w/named entry point)"
when
    $e : WithdrawalEvent($accountNumber : accountNumber, $amount : amount) over window:time(30s) 
    		from entry-point "stream"
    $f : FraudWarningEvent(accountNumber == $accountNumber) over window:time(30s) 
    		from entry-point "stream"    
then
	System.out.println("e=" + $e.id + ", f=" + $f.id)
	results.put("ep.accountNumber", $accountNumber);
	results.put("ep.amount", $amount);
end
