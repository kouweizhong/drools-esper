package memelet.droolsesper

import java.util.Map
dialect "mvel"

/*
 How do I correlate events arriving in 2 or more streams?
 
 http://esper.codehaus.org/tutorials/solution_patterns/solution_patterns.html#correlate
 
	select fraud.accountNumber as accntNum, withdraw.amount as amount         
	from FraudWarningEvent.win:time(30 sec) as fraud,
    	WithdrawalEvent.win:time(30 sec) as withdraw
	where fraud.accountNumber = withdraw.accountNumber
*/	

global Map results

declare WithdrawalEvent
	@role(event)
	id            : String
	accountNumber : String
	amount        : Integer
end

declare FraudWarningEvent
	@role(event)
	id            : String
	accountNumber : String
end

rule "Correlate events arriving in 2 or more streams?"
when
    $e : WithdrawalEvent($accountNumber : accountNumber, $amount : amount) over window:time(30s) 
    $f : FraudWarningEvent(accountNumber == $accountNumber) over window:time(30s) 
then
	results.put("accountNumber", $accountNumber);
	results.put("amount", $amount);
end

rule "Correlate events arriving in 2 or more streams? (w/named entry point)"
when
    $e : WithdrawalEvent($accountNumber : accountNumber, $amount : amount) over window:time(30s) 
    		from entry-point "stream"
    $f : FraudWarningEvent(accountNumber == $accountNumber) over window:time(30s) 
    		from entry-point "stream"    
then
	results.put("ep.accountNumber", $accountNumber);
	results.put("ep.amount", $amount);
end
